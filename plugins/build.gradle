apply plugin: 'com.android.library'

/* To rebuild the plugins this has to be set to true. Otherwise the prebuilt plugins library will be used.
 * This is only for the purpose of providing the Example App without the requirement of installing the Android NDK.
 */
def rebuildPlugins = false

/* Defines the temporary path for the plugins shared library.*/
def wikitudeTmpPath = "${buildDir}/t/libs"

/* Creates a new configuration to extract the shared library from the Wikitude SDK for the Plugins to link against. */
configurations { libraryExtraction }

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared",
                        /* Provides the path to the extracted libarchitect.so to CMake */
                        "-DWIKITUDE_NATIVE_PATH=${wikitudeTmpPath}/jni",
                        /* Provides the path to that the built plugins library should be copied to to CMake */
                        "-DPLUGIN_OUTPUT_PATH=${wikitudeTmpPath}"

                cppFlags "-std=c++14 -Wno-inconsistent-missing-override"
            }
        }
    }

    flavorDimensions "arch"

    /* Defines the product flavor to only build for one or all architectures.
     * The Wikitude SDK supports armeabi-v7a, arm64-v8a and x86.
     */
    productFlavors {
        arm7 {
            dimension "arch"
            externalNativeBuild {
                cmake {
                    abiFilters 'armeabi-v7a'
                }
            }
        }
        arm8 {
            dimension "arch"
            externalNativeBuild {
                cmake {
                    abiFilters 'arm64-v8a'
                }
            }
        }
        x86 {
            dimension "arch"
            externalNativeBuild {
                cmake {
                    abiFilters 'x86'
                }
            }
        }
        allarchs {
            dimension "arch"
            externalNativeBuild {
                cmake {
                    abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
                }
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/cpp/lib']
            /* If the plugins are not rebuilt use the pre-built library that is located in the jniLibs folder. */
            if (!rebuildPlugins) {
                jniLibs.srcDirs += ['src/main/jniLibs']
            }
        }
    }

    if (rebuildPlugins) {
        externalNativeBuild {
            cmake {
                path "src/main/cpp/CMakeLists.txt"
            }
        }
    }
}

dependencies {
    implementation (name: 'wikitudesdk', ext:'aar')
}

repositories {
    flatDir{
        dirs 'libs'
    }
}

/* Task to extract the nativesdk shared library from the aar. */
task extractNativeLibraries() {
    doFirst {
        configurations.libraryExtraction.files.each { file ->
            copy {
                from zipTree(file)
                into wikitudeTmpPath
                include "jni/**/*"
            }
        }
    }
}
tasks.whenTaskAdded {
    task ->
        if (task.name.contains("external") && !task.name.contains("Clean")) {
            /* The externalNativeBuild depends on the extraction task to be able to link properly. */
            task.dependsOn(extractNativeLibraries)
            /* Once the native build is done the plugin library is copied to jniLibs to be available
             * when the plugins should not be rebuilt.
             */
            task.doLast {
                copy {
                    from wikitudeTmpPath
                    into 'src/main/jniLibs'
                }
            }
        }
}